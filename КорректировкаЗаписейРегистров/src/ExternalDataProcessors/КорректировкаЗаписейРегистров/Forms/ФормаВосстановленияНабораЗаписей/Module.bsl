
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого СтрокаСохраненныхНаборовПереданная Из Параметры.СохраненныеНаборыЗаписей Цикл
		ЗаполнитьЗначенияСвойств(СохраненныеНаборыЗаписей.Добавить(), СтрокаСохраненныхНаборовПереданная);
	КонецЦикла;
	
	СохраненныеНаборыЗаписей.Сортировать("ВремяСохранения УБЫВ");
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьВыбранныйНабор(Команда)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.СохраненныеНаборыЗаписей.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбран набор";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ПолноеИмяРегистра", ТекущиеДанные.ПолноеИмяРегистра);
	ПараметрыЗакрытия.Вставить("АдресВоВременномХранилище", ТекущиеДанные.АдресВоВременномХранилище);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьИзФайла(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = "*.xml | *.xml";
	ДиалогВыбораФайла.Заголовок = "Выберите сохраненный набор записей";
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ДиалогВыбораФайла.ПолноеИмяФайла), ВладелецФормы.УникальныйИдентификатор);
		
		ПараметрыЗакрытия = Новый Структура;
		ПараметрыЗакрытия.Вставить("ПолноеИмяРегистра", Неопределено);
		ПараметрыЗакрытия.Вставить("АдресВоВременномХранилище", АдресВоВременномХранилище);
		
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли; 
	
КонецПроцедуры
